group 'jukebot'
version getBuildVersion()

buildscript {
    ext.kotlin_version = '1.7.0'
    ext.kotlin_coroutines = '1.6.2'
    ext.logger_version = '2.17.2'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'jukebot.JukeBot'

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven {
        url 'https://m2.dv8tion.net/releases'
        name 'm2-dv8tion'
    }
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/natanbc/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation ('net.dv8tion:JDA:4.4.0_352') {
        exclude module: 'opus-java'
    }
    implementation 'club.minnced:discord-webhooks:0.8.0'

    /* Audio */
    //implementation 'com.github.devoxin:walkyst-lavaplayer:f4ec4f3'
    implementation 'com.github.walkyst:lavaplayer-fork:1.3.98.4'
    implementation 'com.github.walkyst.lavaplayer-fork:lavaplayer-ext-youtube-rotator:1.3.98.4'
    implementation 'com.sedmelluq:jda-nas:1.1.0'

    /* Database */
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'redis.clients:jedis:4.2.3'

    /* Command Framework */
    implementation 'com.google.guava:guava:31.1-jre'

    /* Logging */
    implementation "org.apache.logging.log4j:log4j-core:$logger_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$logger_version"
    implementation 'io.sentry:sentry:1.7.30'

    /* Kotlin stdlib */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlin_coroutines"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /* Eval */
    implementation "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jsr223:$kotlin_version"

    implementation 'com.grack:nanojson:1.7'
    implementation 'org.jsoup:jsoup:1.14.3'
}

shadowJar {
    archiveFileName = "JukeBot.jar"
    from("src/main/Resources")
}

wrapper {
    gradleVersion = '6.3'
}

def getBuildVersion() {
    def gitVersion = new ByteArrayOutputStream()
    exec {
        commandLine("git", "rev-parse", "--short", "HEAD")
        standardOutput = gitVersion
    }
    return gitVersion.toString().trim()
}

task writeVersion() {
    def resourcePath = sourceSets.main.resources.srcDirs[0]
    def resources = file(resourcePath)

    if (!resources.exists()) {
        resources.mkdirs()
    }

    file("$resourcePath/version.txt").text = getBuildVersion()
}

build.dependsOn(writeVersion)
