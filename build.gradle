group 'me.devoxin.jukebot'
version getBuildVersion()

buildscript {
    ext.kotlin_version = '1.9.22'
    ext.kotlin_coroutines = '1.7.3'
    ext.logger_version = '2.17.2'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'me.devoxin.jukebot.JukeBot'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

/*tasks.named('compileKotlin', KotlinCompilationTask) {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_11)
}*/

repositories {
    maven {
        url 'https://m2.dv8tion.net/releases'
        name 'm2-dv8tion'
    }
    mavenCentral()
    jcenter()
    //maven { url 'https://dl.bintray.com/natanbc/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    final String lavaplayerVersion = "4a16df2"
    final String udpqueueVersion = "0.2.7"

    final List<String> udpqueueModules = [
        "linux-x86-64", "linux-x86", "linux-aarch64", "linux-arm", "linux-musl-x86-64", "linux-musl-aarch64", "win-x86-64", "win-x86", "darwin"
    ]

    implementation ('net.dv8tion:JDA:5.0.0-beta.20') {
        exclude module: 'opus-java'
    }

    implementation "com.github.devoxin:flight:572acd6"

    // Audio
    implementation "com.github.devoxin.lavaplayer:lavaplayer:$lavaplayerVersion"
    implementation "com.github.devoxin.lavaplayer:lavaplayer-ext-youtube-rotator:$lavaplayerVersion"
    implementation ('com.sedmelluq:jda-nas:1.1.0') {
        exclude module: 'udp-queue'
    }
    udpqueueModules.forEach {
        implementation "club.minnced:udpqueue-native-$it:$udpqueueVersion"
    }

    // Audio Filters
    implementation 'com.github.devoxin:LavaDSPX:2.0.1'
    implementation 'com.github.natanbc:lavadsp:0.7.7'

    // Database
    implementation 'org.xerial:sqlite-jdbc:3.43.2.1'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'redis.clients:jedis:5.0.0'

    // Logging
    implementation "org.apache.logging.log4j:log4j-core:$logger_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$logger_version"
    implementation 'io.sentry:sentry:1.7.30'

    // stdlib (Kotlin)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlin_coroutines"

    // Utilities
    implementation 'com.grack:nanojson:1.7'
    implementation 'org.jsoup:jsoup:1.16.2'
    implementation 'commons-cli:commons-cli:1.6.0'

    // Transitive overrides
    implementation "org.json:json:20231013"
    implementation "commons-codec:commons-codec:1.13"
    implementation "com.squareup.okio:okio-jvm:3.4.0"
}

shadowJar {
    archiveFileName = "JukeBot.jar"
    from("src/main/Resources")
}

def getBuildVersion() {
    def gitVersion = new ByteArrayOutputStream()
    exec {
        commandLine("git", "rev-parse", "--short", "HEAD")
        standardOutput = gitVersion
    }
    return gitVersion.toString().trim()
}

tasks.register('writeVersion') {
    def resourcePath = sourceSets.main.resources.srcDirs[0]
    def resources = file(resourcePath)

    if (!resources.exists()) {
        resources.mkdirs()
    }

    file("$resourcePath/version.txt").text = getBuildVersion()
}

build.dependsOn(writeVersion)
